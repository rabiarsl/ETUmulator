apply plugin: "application"
apply plugin: "jacoco"
apply plugin: "antlr"
apply plugin: "pmd"
apply plugin: "findbugs"

version = "0.5.0"
sourceCompatibility = "1.8"
targetCompatibility = "1.8"

[compileJava, compileTestJava]*.options*.encoding = "UTF-8"

mainClassName = "com.kasirgalabs.etumulator.ETUmulator"

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/typesafe/maven-releases" }
}

configurations {
    providedRuntime
    codacy
}

dependencies {
    antlr group: "org.antlr", name: "antlr4", version: "4.7"
    testCompile group: "junit", name: "junit", version: "4.12"
    compile group: "com.google.inject", name: "guice", version: "4.1.0"
    compile group: "org.fxmisc.richtext", name: "richtextfx", version: "0.7-M3"
    codacy group: "com.codacy", name: "codacy-coverage-reporter", version: "2.0.0"
}

tasks.withType(Test) {
    testLogging {
        events "FAILED", "PASSED", "SKIPPED", "STANDARD_ERROR"
        showCauses = true
        showExceptions = true
        showStackTraces = true
        showStandardStreams = true
        afterSuite { desc, result ->
            if(!desc.parent) {
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = "|  ", endItem = "  |"
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println("\n" + ("-" * repeatLength) + "\n" + startItem + output + endItem + "\n" + ("-" * repeatLength))
            }
        }
    }
}

pmd {
    toolVersion = "5.5.5"
    consoleOutput = true
    ignoreFailures = true
    ruleSetFiles = files("config/pmd/kasirgalabs-pmd.xml")
}

tasks.withType(Pmd) {
    reports {
        html.enabled true
        xml.enabled true
    }
}

findbugs {
    toolVersion = "3.0.1"
    ignoreFailures = true
    effort = "max"
    reportLevel = "low"
}

tasks.withType(FindBugs) {
    reports {
        html.enabled = true
        xml.enabled = false
        xml.withMessages = true
    }
}

generateGrammarSource {
    arguments += ["-package", "com.kasirgalabs.thumb2", "-visitor", "-no-listener", "-long-messages"]
    doLast {
        copy {
            from "build/generated-src/antlr/main/"
            include "*.java"
            into "src/main/java/com/kasirgalabs/thumb2"
        }
        delete "build/generated-src/antlr"
    }
}

task uploadCoverageToCodacy(type: JavaExec, dependsOn : jacocoTestReport) {
    main = "com.codacy.CodacyCoverageReporter"
    classpath = configurations.codacy
    args = [
            "-l",
            "Java",
            "-r",
            "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    ]
}

jar {
    manifest {
        attributes "Application-Name": "ETUmulator"
        attributes "Implementation-Title": "Simple Thumb-2 Emulator"
        attributes "Implementation-Version": version
        attributes "Implementation-Vendor": "Kasirgalabs"
        attributes "Main-Class": mainClassName
        attributes "Codebase": "https://github.com/kasirgalabs/ETUmulator/releases"
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

jacoco {
    toolVersion = "0.7.9"
}

jacocoTestReport {
    afterEvaluate {
        classDirectories = files(
            classDirectories.files.collect {
                fileTree(dir: it, exclude: "com/kasirgalabs/thumb2/**")
            })
    }
    reports {
        html.enabled true
        xml.enabled true
        csv.enabled true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.10"
}
